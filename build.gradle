buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
    ext {
        common_lang3_version = '3.4'
        protobuf_version = '3.9.0'
        junit_version = '5.5.2'
        lombok_version = '1.18.4'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'com.google.protobuf' version '0.8.10'
    id 'maven'
    id 'signing'
}

group = 'com.keithtmiller'
archivesBaseName = 'proto-converter'
version = '1.0.1'

sourceCompatibility = 11.0
targetCompatibility = 11.0

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    // annotations needed for protobuf generated code
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: "${common_lang3_version}"

    // protobuf dependencies
    api group: 'com.google.protobuf', name: 'protobuf-java', version: "${protobuf_version}"

    // test dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: "${junit_version}"
    testCompile group: 'org.projectlombok', name: 'lombok', version: "${lombok_version}"
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombok_version}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobuf_version}"
    }
}

sourceSets {
    main {
        java {
            srcDirs "${projectDir}/build/generated/source/proto/main/java"
            srcDirs "${projectDir}/build/generated/source/proto/main/grpc"
            srcDirs "${projectDir}/src/main/java"
        }
    }
}

test {
    useJUnitPlatform()

    exclude 'com/keithtmiller/prototest/**'
    exclude 'com/keithtmiller/protoconverter/example/**'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Proto-Converter'
                packaging 'jar'
                description 'An Annotation based Java library for converting classes (like DTOs) to Protobuf classes'
                url 'https://github.com/keith-miller/proto-converter'

                scm {
                    connection 'https://github.com/keith-miller/proto-converter.git'
                    developerConnection 'https://github.com/keith-miller/proto-converter.git'
                    url 'https://github.com/keith-miller/proto-converter.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'keith-miller'
                        name 'Keith Miller'
                        email 'keeftm@gmail.com'
                    }
                }
            }
        }
    }
}

